/-
SHA-256 (FIPS 180-4) over byte arrays, used by the validator drivers
to bind a machine-readable response to the exact artifact bytes it was
computed from. Untrusted plumbing: a wrong hash here can only make a
response fail to match the caller's independently computed hash.
-/
namespace Rwv.Sha256

private def k : Array UInt32 := #[
  0x428a2f98, 0x71374491, 0xb5c0fbcf, 0xe9b5dba5,
  0x3956c25b, 0x59f111f1, 0x923f82a4, 0xab1c5ed5,
  0xd807aa98, 0x12835b01, 0x243185be, 0x550c7dc3,
  0x72be5d74, 0x80deb1fe, 0x9bdc06a7, 0xc19bf174,
  0xe49b69c1, 0xefbe4786, 0x0fc19dc6, 0x240ca1cc,
  0x2de92c6f, 0x4a7484aa, 0x5cb0a9dc, 0x76f988da,
  0x983e5152, 0xa831c66d, 0xb00327c8, 0xbf597fc7,
  0xc6e00bf3, 0xd5a79147, 0x06ca6351, 0x14292967,
  0x27b70a85, 0x2e1b2138, 0x4d2c6dfc, 0x53380d13,
  0x650a7354, 0x766a0abb, 0x81c2c92e, 0x92722c85,
  0xa2bfe8a1, 0xa81a664b, 0xc24b8b70, 0xc76c51a3,
  0xd192e819, 0xd6990624, 0xf40e3585, 0x106aa070,
  0x19a4c116, 0x1e376c08, 0x2748774c, 0x34b0bcb5,
  0x391c0cb3, 0x4ed8aa4a, 0x5b9cca4f, 0x682e6ff3,
  0x748f82ee, 0x78a5636f, 0x84c87814, 0x8cc70208,
  0x90befffa, 0xa4506ceb, 0xbef9a3f7, 0xc67178f2]

private def rotr (x : UInt32) (n : Nat) : UInt32 :=
  (x >>> UInt32.ofNat n) ||| (x <<< UInt32.ofNat (32 - n))

/-- The message padded to a multiple of 64 bytes: 0x80, zeros, and the
bit length as a big-endian 64-bit tail. -/
private def pad (msg : ByteArray) : ByteArray := Id.run do
  let bitLen : UInt64 := UInt64.ofNat (msg.size * 8)
  let mut out := msg.push 0x80
  while out.size % 64 ≠ 56 do
    out := out.push 0
  for i in [0:8] do
    out := out.push (UInt8.ofNat (((bitLen >>> UInt64.ofNat ((7 - i) * 8)) &&& 0xff).toNat))
  return out

private def word (b : ByteArray) (off : Nat) : UInt32 :=
  (UInt32.ofNat (b.get! off).toNat <<< 24)
  ||| (UInt32.ofNat (b.get! (off + 1)).toNat <<< 16)
  ||| (UInt32.ofNat (b.get! (off + 2)).toNat <<< 8)
  ||| UInt32.ofNat (b.get! (off + 3)).toNat

/-- The digest of a byte array, as eight 32-bit words. -/
def digestWords (msg : ByteArray) : Array UInt32 := Id.run do
  let b := pad msg
  let mut h : Array UInt32 := #[
    0x6a09e667, 0xbb67ae85, 0x3c6ef372, 0xa54ff53a,
    0x510e527f, 0x9b05688c, 0x1f83d9ab, 0x5be0cd19]
  for blk in [0:b.size / 64] do
    let base := blk * 64
    let mut w : Array UInt32 := Array.mkEmpty 64
    for i in [0:16] do
      w := w.push (word b (base + i * 4))
    for i in [16:64] do
      let s0 := rotr w[i-15]! 7 ^^^ rotr w[i-15]! 18 ^^^ (w[i-15]! >>> 3)
      let s1 := rotr w[i-2]! 17 ^^^ rotr w[i-2]! 19 ^^^ (w[i-2]! >>> 10)
      w := w.push (w[i-16]! + s0 + w[i-7]! + s1)
    let mut a := h[0]!
    let mut bb := h[1]!
    let mut c := h[2]!
    let mut d := h[3]!
    let mut e := h[4]!
    let mut f := h[5]!
    let mut g := h[6]!
    let mut hh := h[7]!
    for i in [0:64] do
      let s1 := rotr e 6 ^^^ rotr e 11 ^^^ rotr e 25
      let ch := (e &&& f) ^^^ (~~~e &&& g)
      let t1 := hh + s1 + ch + k[i]! + w[i]!
      let s0 := rotr a 2 ^^^ rotr a 13 ^^^ rotr a 22
      let mj := (a &&& bb) ^^^ (a &&& c) ^^^ (bb &&& c)
      let t2 := s0 + mj
      hh := g; g := f; f := e; e := d + t1
      d := c; c := bb; bb := a; a := t1 + t2
    h := #[h[0]! + a, h[1]! + bb, h[2]! + c, h[3]! + d,
           h[4]! + e, h[5]! + f, h[6]! + g, h[7]! + hh]
  return h

private def hexDigit (n : Nat) : Char :=
  if n < 10 then Char.ofNat ('0'.toNat + n) else Char.ofNat ('a'.toNat + n - 10)

private def wordHex (w : UInt32) : String := Id.run do
  let mut s := ""
  for i in [0:8] do
    s := s.push (hexDigit (((w >>> UInt32.ofNat ((7 - i) * 4)) &&& 0xf).toNat))
  return s

/-- The lowercase hex digest of a byte array. -/
def hex (msg : ByteArray) : String :=
  String.join ((digestWords msg).toList.map wordHex)

/-- The lowercase hex digest of a string's UTF-8 bytes. -/
def hexOfString (s : String) : String := hex s.toUTF8

end Rwv.Sha256
